apply plugin: 'com.android.application'

def versionDesc() {
    def text = 'No version info\n'
    try {
        def proc = 'git describe --always --tags --dirty'.execute()
        proc.waitFor()
        if (proc.exitValue() == 0) {
            text = proc.text.trim()
            println('Version info: git-describe = ' + text.trim())
        } else {
            logger.warn('Warning: git describe failed.')
        }
    } catch (e) {
        logger.warn('Warning: git command failed.')
        logger.warn('Warning: Ensure that git command is available.')
    }
    text
}

android {
    signingConfigs {
        release {
            storeFile file('../sign/dummy_keystore.jks')
            storePassword 'dummypass'
            keyAlias 'dummy_key'
            keyPassword 'dummypass'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "io.github.yappy.annplayer"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName versionDesc()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.android.support:design:28.0.0'
}
